<!DOCTYPE html>
<html>
<head>
  <script src="../../processing.js"></script>
<link rel="stylesheet" href="../style.css"/></head>
<body><h1><a href="http://ejohn.org/blog/processingjs/">Processing.js</a></h1>
<h2>Follow3</h2>

<p>Based on code from Keith Peters (www.bit-101.com). 

A segmented line follows the mouse. The relative angle from
each segment to the next is calculated with atan2() and the
position of the next is calculated with sin() and cos().</p>

<p><a href="http://processing.org/learning/topics/follow3.html"><b>Original Processing.org Example:</b> Follow3</a><br>
<script type="application/processing">
float[] x = new float[20];
float[] y = new float[20];
float segLength = 9;

void setup() {
  size(200, 200);
  smooth(); 
  strokeWeight(5);
  stroke(0, 100);
}

void draw() {
  background(226);
  dragSegment(0, mouseX, mouseY);
  for(int i=0; i<x.length-1; i++) {
    dragSegment(i+1, x[i], y[i]);
  }
}

void dragSegment(int i, float xin, float yin) {
  float dx = xin - x[i];
  float dy = yin - y[i];
  float angle = atan2(dy, dx);  
  x[i] = xin - cos(angle) * segLength;
  y[i] = yin - sin(angle) * segLength;
  segment(x[i], y[i], angle);
}

void segment(float x, float y, float a) {
  pushMatrix();
  translate(x, y);
  rotate(a);
  line(0, 0, segLength, 0);
  popMatrix();
}
</script><canvas width="200" height="200"></canvas></p>
<div style="height:0px;width:0px;overflow:hidden;"></div>

<pre><b>// All Examples Written by <a href="http://reas.com/">Casey Reas</a> and <a href="http://benfry.com/">Ben Fry</a>
// unless otherwise stated.</b>
float[] x = new float[20];
float[] y = new float[20];
float segLength = 9;

void setup() {
  size(200, 200);
  smooth(); 
  strokeWeight(5);
  stroke(0, 100);
}

void draw() {
  background(226);
  dragSegment(0, mouseX, mouseY);
  for(int i=0; i&lt;x.length-1; i++) {
    dragSegment(i+1, x[i], y[i]);
  }
}

void dragSegment(int i, float xin, float yin) {
  float dx = xin - x[i];
  float dy = yin - y[i];
  float angle = atan2(dy, dx);  
  x[i] = xin - cos(angle) * segLength;
  y[i] = yin - sin(angle) * segLength;
  segment(x[i], y[i], angle);
}

void segment(float x, float y, float a) {
  pushMatrix();
  translate(x, y);
  rotate(a);
  line(0, 0, segLength, 0);
  popMatrix();
}</pre>
</body>
</html>
